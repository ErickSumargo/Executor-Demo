def plugins = [
    "com.android.application",
    "kotlin-android",
    "kotlin-android-extensions",
    "kotlin-kapt",
    "androidx.navigation.safeargs.kotlin",
    "dagger.hilt.android.plugin"
]

plugins.forEach { plugin ->
    apply plugin: plugin
}

android {
    compileSdkVersion sdk.compile

    defaultConfig {
        applicationId "com.bael.executor.demo"
        minSdkVersion sdk.min
        targetSdkVersion sdk.target
        versionCode sdk.code
        versionName sdk.version

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments["dagger.hilt.disableModulesHaveInstallInCheck"] = "true"
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    buildFeatures {
        viewBinding = true
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8
    }
}

def deps = [
    "appcompat",
    "assistedinject",
    "constraintlayout",
    "fragment",
    "hilt",
    "lifecycle",
    "navigation"
]

deps.forEach { dep ->
    apply from: "$rootDir/deps/${dep}.gradle"
}

def modules = [
    "lib_base",
    "lib_threading",
]

dependencies {
    modules.forEach { module ->
        implementation project(":$module")
    }
}
